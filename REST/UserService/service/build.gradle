apply plugin: 'java'
apply plugin: 'war'

    repositories {
        maven {url "https://github.com/s7/mvnrepo/raw/master"}
    }


sourceSets {
    main {
        java { srcDir 'src/main' }
        resources { srcDir 'src/main' }
    }
    test {
        java { srcDir 'src/test' }
        resources { srcDir 'src/test' }
    }
}

jar { from sourceSets.main.allJava }

// ========= Environment ================================================

import org.apache.tools.ant.filters.ReplaceTokens

if (project.hasProperty('env')) {
    def env = project.getProperty("env")

    task copyEnvConfigApp(type: Copy) {
        from 'appconfig'
        into "$System.env.TOMCAT_HOME/conf/appconfig"
        include '**/*.properties'
        include '**/*.xml'
        exclude '**/*.bat'
        exclude '**/*.sh'
    }

    task copyEnvConfigTomcat(type: Copy) {
        from "appconfig/env/$env"
        into "$System.env.TOMCAT_HOME/bin"
        include '**/*.bat'
        include '**/*.sh'
    }

    task copyWebContextConfig(type: Copy) {
        from "appconfig/UserService.xml"
        into "$System.env.TOMCAT_HOME/conf/Catalina/localhost"
        filter(ReplaceTokens, tokens: [war: war.archiveName])
    }

    task setupEnv {
        dependsOn copyEnvConfigApp, copyEnvConfigTomcat, copyWebContextConfig
    }
}

// ========== Deploy ====================================================
war {
    manifest {
        attributes(
                'Product': rootProject.name,
                'Version': version,
                'Built-On': new Date().format('yyyy-mm-dd HH:MM:ss')
        )
    }
}

task deploy(type: Copy) {
    dependsOn war
    if (project.hasProperty('env')) {
        dependsOn setupEnv
    }
    from war
    into "$System.env.TOMCAT_HOME/warfiles"
}

// ========== Clean Deployment Configuration ============================

task cleanTomcat(type: Delete) {
    FileTree tree = fileTree("$System.env.TOMCAT_HOME") {
        include "conf/Catalina/localhost/UserService.xml",
                "warfiles/ru.forxy.user*.war",
                "conf/appconfig/**/ru.forxy.user*.properties"
    }
    delete tree
}

// ========== Tests =====================================================

test {
    reports.html.enabled = true
    maxParallelForks = 8
}

// ========== Dependencies ==============================================

dependencies {
    // --------------------- FORXY ---------------------------------------
    compile project(":ru.forxy.user.api")
    compile "forxy:ru.forxy.user.api:${rev_forxy_common}"
    compile "forxy:ru.forxy.common.rest:${rev_forxy_common}"
    compile "forxy:ru.forxy.common.utils:${rev_forxy_common}"
    compile "forxy:ru.forxy.common.status:${rev_forxy_common}"
    compile "forxy:ru.forxy.common.logging:${rev_forxy_common}"

    // --------------------- JAX-WS --------------------------------------
    compile "org.apache.cxf:cxf-rt-frontend-jaxws:${rev_cxf}"
    compile "org.apache.cxf:cxf-rt-transports-http:${rev_cxf}"

    // --------------------- COMMON --------------------------------------
    compile "commons-collections:commons-collections:${rev_common_collections}"
    compile "org.slf4j:slf4j-api:${rev_slf4j}"
    compile "ch.qos.logback:logback-core:${rev_logback}"
    compile "ch.qos.logback:logback-classic:${rev_logback}"

    // --------------------- MONGO ---------------------------------------
    compile "org.mongodb:mongo-java-driver:${rev_mongo}"
    compile "org.springframework.data:spring-data-mongodb:${rev_spring_mongo}"

    // --------------------- CASSANDRA -----------------------------------
    compile ("com.impetus.core:kundera-core:${rev_kundera_cassandra}") {
        exclude module: 'persistence-api'
    }
    compile ("com.impetus.client:kundera-cassandra:${rev_kundera_cassandra}") {
        exclude module: 'snappy'
        exclude module: 'persistence-api'
    }
    compile "org.xerial.snappy:snappy-java:${rev_snappy}"
    compile "org.hibernate.javax.persistence:hibernate-jpa-2.0-api:${rev_persistance}"

    // --------------------- SPRING --------------------------------------
    compile "org.springframework:spring-core:${rev_spring}"
    compile "org.springframework:spring-beans:${rev_spring}"
    compile "org.springframework:spring-context:${rev_spring}"
    compile "org.springframework:spring-aop:${rev_spring}"
    compile "org.springframework:spring-web:${rev_spring}"
    compile "org.springframework:spring-orm:${rev_spring}"

    // ===================== TEST ========================================
    testCompile "org.springframework:spring-test:${rev_spring}"
    testCompile "junit:junit:${rev_junit}"
    testCompile "org.easymock:easymock:${rev_easymock}"
}

eclipse {
    project { name = 'ru.forxy.user.service' }
}